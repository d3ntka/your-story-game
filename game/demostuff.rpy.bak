################################################################################
#############################   EPIZOD 7-N   ###################################
label epizod7n:
    if config.developer:
        scene black
        show text "epizod7n" at truecenter
        pause
    # Na ekranie: zewnątrz Tawerny, noc w dzień pyk animowane takie ładne
    scene anim_room_lia_goldenhour with dissolve
    pause 1
    show anim_room_lia_nightdragonlight with Dissolve(2.0)
    pause 1
    show anim_room_lia_nightdragon with Dissolve(2.0)
    pause 2
    show anim_room_lia_morning with Dissolve(2.0)
    # Na ekranie: Pokój P1
    scene anim_room_lia_morning
    show room_lia_letter_idle
    with dissolve
    "Lia przebudziła się dość wcześnie, wyjątkowo wypoczęta."
    $ gui.main_menu_background = "gui/other/main_menu_end.png"
    $ gui.rebuild()
    "Wczorajsza decyzja o wczesnym pójściu spać najwidoczniej już się opłaciła."
    "Usiadła na łóżku. Przeciągając się bezgłośnie, była ciekawa zbliżającego się śniadania."
    # TODO Na ekranie: Notatka nt. zakończenia demo i info co dalej
    jump demoepilog

################################################################################
#############################   DEMO-EPILOG   ##################################
label demoepilog:
    #if config.developer:
    #    scene black
    #    show text "epilog" at truecenter
    #    pause
    play music alexander_nakarada_battle_of_the_creek fadein 3.0
    $ gui.main_menu_background = "gui/other/main_menu_end.png"
    $ gui.rebuild()
    call screen thankyouforplaying

    if _return == "goexplore":
        call screen sc_pre_goexplore with dissolve
        call screen sc_goexplore with dissolve
    if _return == "mapa":
        jump demo_mapa
        #call screen sc_demo_mapka
    return

###########  SCREEN Z KARTECZKA Z PODZIEKOWANIAMI ORAZ PRZEJSCIE DALEJ #########
image exit_button_idle = im.FactorScale("gui/button/choice_idle_background.png", 0.4)
image exit_button_hover = im.FactorScale("gui/button/choice_hover_background.png", 0.4)
screen thankyouforplaying():
    add "demoend"
    imagebutton auto "demoend_steambutton_%s" focus_mask True action OpenURL("https://store.steampowered.com/app/1247740/Your_Story")
    imagebutton auto "demoend_ksbutton_%s" focus_mask True action OpenURL("https://www.kickstarter.com/projects/gameload/yourstory")

    imagebutton:
        align (0.5,0.93) #lewa
        xpos .2
        idle "exit_button_idle"
        hover "exit_button_hover"
        action Return()
    text "EXIT" align (0.5,0.919) xpos 0.20 #lewa

    imagebutton:
        align (0.5,0.87) #lewa
        xpos .2
        idle "exit_button_idle"
        hover "exit_button_hover"
        action Return("goexplore")
    text "EXPLORE" align (0.5,0.859) xpos 0.20 #lewa

screen sc_pre_goexplore():
    frame:
        xpadding 100
        ypadding 50
        xalign 0.5
        yalign 0.5
        text _("W tym momencie odblokowany został tryb eksploracji. Żeby przejść do mapy świata wystarczy kliknąć mapę znajdującą się w prawym dolnym rogu. Podróżujecie klikając na wybraną lokację.\n\nTryb eksploracji jest dostępny w wersji demonstracyjnej - to co zobaczycie teraz to tylko przedsmak tego co czeka Was w pełnej wersji gry. \n\nNie zapomnijcie podzielić się Waszą opinią na naszych forach! Dzięki!")

    imagebutton:
        align (0.5,0.77) #lewa
        idle "exit_button_idle"
        hover "exit_button_hover"
        action Return()
    text "GOT IT!" align (0.5,0.759) #lewa

screen sc_goexplore():
    modal True
    imagebutton auto "button_map_%s" focus_mask True action Return("mapa")



################################################################################


################################################################################
##############################   DEMO-MAPKA   ##################################
screen sc_demo_mapka():
    default tw = Tooltip("")
    tag demo_map
    modal True
    add "mapka_fhd"
    imagebutton: #monument
         focus_mask True
         idle "monument idle"
         hover "monument_hover"
         action Jump("demo_monument")
    imagebutton: #wioska
         focus_mask True
         idle "wioska idle"
         hover "wioska_hover"
         action Jump("demo_wioska")
    imagebutton: #tawerna
         focus_mask True
         idle "tawerna idle"
         hover "tawerna_hover"
         action Jump("demo_tawerna")
    imagebutton: #drzewo
         focus_mask True
         hovered tw.Action(_("Dostępne w pełnej wersji."))
         idle "drzewo idle"
         hover "drzewo_hover"
         action NullAction()
    text tw.value color "#fff" xalign 0.0 yalign 0.5 xpos 0.7 ypos 0.19 size 32
########################## ELEMENTY DO DEMO-WIOSKA #############################
image festival_day_hd_reszta:
    contains:
        "festival_day_hd"
        zoom 0.46875
    contains:
        "festival_day_reszta"

image festival_day_hd:
    contains:
        "festival_day_dragonsky"
    contains:
        "festival_dragon"
        align (.5,.5) pos (1.07,0.70)
        choice:
            linear 3.0 pos (1.08,0.70)
        choice:
            linear 3.0 pos (1.06,0.69)
        choice:
            linear 3.0 pos (1.06,0.72)
        choice:
            linear 3.0 pos (1.08,0.69)
        linear 5.0 pos (1.07,0.70)
        repeat
    contains:
        "festival_day_firstplannoppl"

image festival_day_reszta:
    contains:
        "festival_wiszonce_idle"
    contains:
        "festival_namioty_idle"
    contains:
        "festival_sketchy_idle"
    contains:
        "festival_family_idle"
    contains:
        "festival_busypeople_idle"
    contains:
        "festival_drunky_idle"
    contains:
        "festival_ladies_idle"
    contains:
        "festival_cat_idle"
    contains:
        "festival_backers_idle"
    contains:
        "festival_guard_idle"
    contains:
        "festival_szkrzynka_idle"
    contains:
        "festival_swiatelka_idle"

default z_wiszonce = True
default z_namioty = True
default z_sketchy = True
default z_family = True
default z_busypeople = True
default z_drunky = True
default z_ladies = True
default z_cat = True
default z_backers = True
default z_szkrzynka = True
default z_swiatelka = True

################################# SCREEN-DEMO-WIOSKA ###################################
screen sc_demo_wioska():
    tag demo_map
    modal True
    if z_wiszonce:
        imagebutton auto "festival_wiszonce_%s" focus_mask True action [ToggleVariable("z_wiszonce"), Return("wiszonce")]
    else:
        image "festival_wiszonce_idle"

    if z_namioty:
        imagebutton auto "festival_namioty_%s" focus_mask True action [ToggleVariable("z_namioty"), Return("namioty")]
    else:
        image "festival_namioty_idle"

    if z_sketchy:
        imagebutton auto "festival_sketchy_%s" focus_mask True action [ToggleVariable("z_sketchy"), Return("sketchy")]
    else:
        image "festival_sketchy_idle"

    if z_family:
        imagebutton auto "festival_family_%s" focus_mask True action [ToggleVariable("z_family"), Return("family")]
    else:
        image "festival_family_idle"

    if z_busypeople:
        imagebutton auto "festival_busypeople_%s" focus_mask True action [ToggleVariable("z_busypeople"), Return("busypeople")]
    else:
        image "festival_busypeople_idle"

    if z_drunky:
        imagebutton auto "festival_drunky_%s" focus_mask True action [ToggleVariable("z_drunky"), Return("drunky")]
    else:
        image "festival_drunky_idle"

    if z_ladies:
        imagebutton auto "festival_ladies_%s" focus_mask True action [ToggleVariable("z_ladies"), Return("ladies")]
    else:
        image "festival_ladies_idle"

    if z_cat:
        imagebutton auto "festival_cat_%s" focus_mask True action [ToggleVariable("z_cat"), Return("cat")]
    else:
        image "festival_cat_idle"

    #if z_backers:
    #    imagebutton auto "festival_backers_%s" focus_mask True action [ToggleVariable("z_backers"), Return("backers")]
    #else:
    #    image "festival_backers_idle"
    image "festival_backers_idle"

    imagebutton auto "festival_guard_%s" focus_mask True action Return("guard") #guarda mozna klikac kilkakrotnie

    if z_szkrzynka:
        imagebutton auto "festival_szkrzynka_%s" focus_mask True action [ToggleVariable("z_szkrzynka"), Return("szkrzynka")]
    else:
        image "festival_szkrzynka_idle"

    if z_swiatelka:
        imagebutton auto "festival_swiatelka_%s" focus_mask True action [ToggleVariable("z_swiatelka"), Return("swiatelka")]
    else:
        image "festival_swiatelka_idle"

    imagebutton auto "button_map_%s" focus_mask True action Return("mapa")

################################# SCREEN-DEMO-MONUMENT ###################################
screen sc_demo_monument():
    imagebutton auto "guard_monument_%s" focus_mask True action Return("guard")
    imagebutton auto "button_map_%s" focus_mask True action Return("mapa")

################################# SCREEN_DEMO-LIAROOM ###################################
screen sc_demo_liaroom():
    imagebutton auto "room_lia_letter_%s" focus_mask True action Return("letter")
    imagebutton auto "button_map_%s" focus_mask True action Return("mapa")


################################################################################
################################### LABELE #####################################
################################################################################

################################## DEMO-MAPA ###################################
label demo_mapa:
    scene black
    call screen sc_demo_mapka with fade
    #"Choose your destiny"

default strzrozm1 = False
default strzrozm2 = False
################################# DEMO-WIOSKA ###################################
init python:

    def ballgazewio():
        ballwio = renpy.random.randint(1, 5)
        if ballwio == 1:
            return _("Witaj Obywatelko!")
        if ballwio == 2:
            return _("Jak mogę pomóc?")
        if ballwio == 3:
            return _("Hmm... Tak?")
        if ballwio == 4:
            return _("Przepraszam, mówiłaś coś?")
        if ballwio == 5:
            return _("Cholerne Słońce... Ahhh, witam obywatelkę!")

label demo_wioska:
    default z_demo_wioska_1wejscie = False
    if not z_demo_wioska_1wejscie:
        scene forest_village_dragonday:
            zoom 0.46875
        show p1:
            align (0.7,1.0)
        show guard:
            align (0.2,1.0) xzoom -1
        with fade
        "Gdy Lia zbliżyła się do wioski to natychmiast podeszła do niej postać w pełnej zbroi."
        strzwio "Obywatelko! Nim wejdziesz do wioski muszę ostrzec Cię o trwających tam pracach."
        "Lia lekko się zdziwiła, strażnicy nie byli zbyt często widywani w okolicy."
        strzwio "Jak za moment zobaczysz... Wszystko wygląda dość, ekhem... dziwnie."
        strzwio "Powodem są prace jakichś trzech magów. Nic więcej niestety nie wiem."
        strzwio "Prosili informować o tym, żeby nikt nie pomyślał, że to jakiś problem czy coś."
        strzwio "Termin zakończenia prac przewidują na październik."
        #Lia smile ON (tylko na to zdanie)
        show p1 lsmile with fc
        "Lia nie wiedziała za bardzo co odpowiedzieć, więc uśmiechnęła się i ruszyła dalej."
        #Na ekranie: wioska z obiektami do klikania. Przy następnych kliknięciach na wioskę już od razu trafia do tej lokacji
        $ z_demo_wioska_1wejscie = True


    scene festival_day_hd:
        zoom 0.46875
    show festival_day_reszta
    with fade
    call screen sc_demo_wioska

    while _return != "mapa":
        #default tekstguarda = renpy.random.choice(["Witaj Obywatelko!", "Jak mogę pomóc? ", "Hmm... Tak?","Przepraszam, mówiłaś coś?","Cholerne słońce... Ahhh, witam obywatelkę!"])
        default z_demo_wioska_koniecrozmowy = False
        if _return == "wiszonce":
            p1 "Hmm... Szkoda, że smok nie jest różowy. Byłoby wtedy uroczo."
        if _return == "namioty":
            p1 "Ciekawe ile jeszcze potrwa festiwal... Sporo pracy ktoś włożył, żeby było tutaj tak ładnie."
        if _return == "sketchy":
            p1 "Trochę to podejrzane, że tak siedzą w cieniu i nic nie robią..."
        if _return == "family":
            p1 "Nie pamiętam, żebyśmy my całą rodziną gdzieś tak wychodzili..."
        if _return == "busypeople":
            p1 "Każdy w swoim kierunku, każdy z setką obowiązków. Pewnie tak właśnie wyglądam w pracy..."
        if _return == "drunky":
            p1 "Dziwne, że wciąż mieli czelność zostać w okolicy... Odważni. Lub głupi."
        if _return == "ladies":
            p1 "Hmm, całkiem ładne sukienki... "
        if _return == "cat":
            p1 "Ojej! Jaki słodki kotek!"
        if _return == "backers":
            p1 ""


        if _return == "guard":
            #default tekstguarda = renpy.random.choice(["Witaj Obywatelko!", "Jak mogę pomóc?", "Hmm... Tak?","Przepraszam, mówiłaś coś?","Cholerne słońce... Ahhh, witam obywatelkę!"])
            $ tekstguardawio = ballgazewio()
            strzwio "[tekstguardawio!t]"
            $ z_demo_wioska_koniecrozmowy = False
            while not z_demo_wioska_koniecrozmowy:
                menu:
                    "Czy mógłby Pan przypomnieć co tu się stało?":
                        p1 "Czy mógłby Pan przypomnieć co tu się stało?"
                        strzwio "Tak jak wcześniej wspominałem..."
                        strzwio "Ma to związek z pracami trójki magów czy kogoś takiego."
                        strzwio "Co dokładnie robią... Nie mam pojęcia."
                        strzwio "Wiem, że wszystko będzie na swoim miejscu jak skończą."
                        $ strzrozm1 = True

                    "I do kiedy ma ta cała sytuacja tak wyglądać?" if strzrozm1:
                        p1 "I do kiedy ma ta cała sytuacja tak wyglądać?"
                        strzwio "Ah... Całość ma być zakończona do października."
                        $ strzrozm2 = True

                    "Jakaś szansa, że skończą wcześniej?" if strzrozm2:
                        p1 "Jakaś szansa, że skończą wcześniej?"
                        strzwio "Szansa jest zawsze. {a=https://www.kickstarter.com/projects/gameload/yourstory}Tutaj{/a} ulotka jak można wesprzeć całą tę inicjatywę."
                        strzwio "Ale wiemy jak jest, może się uda, a może nie. Trzeba trzymać kciuki."
                        p1 "No tak... Dziękuję w takim razie."
                        # Kończy okno dialogowe i wraca do widoku wioski
                        $ z_demo_wioska_koniecrozmowy = True

                    "Pójdę już sobie.":
                        p1 "Pójdę już sobie."
                        $ z_demo_wioska_koniecrozmowy = True
        if _return == "szkrzynka":
            p1 "Chyba nikt nie był w stanie już tego posprzątać..."
        if _return == "swiatelka":
            p1 "Śliczne są te światełka!"




        call screen sc_demo_wioska with dissolve
    jump demo_mapa
    #call screen sc_demo_mapka with fade


################################ DEMO-MONUMENT #################################
init python:

    def ballgaze():
        ball = renpy.random.randint(1, 5)
        if ball == 1:
            return _("Warta przy drodze... Też mi... Ahh, witam!")
        if ball == 2:
            return _("Jakoś pomóc?")
        if ball == 3:
            return _("Hmm... Czego?")
        if ball == 4:
            return _("Mówiłaś coś?")
        if ball == 5:
            return _("Cholerne Słońce... Eh.. Witam!")

label demo_monument:
    scene village_monumentcovered
    default z_demo_monum_1wejscie = False
    if not z_demo_monum_1wejscie:
        show guard_blue:
            align(1.0,1.0) xpos 1.0215
        show architect angry:
            align(0.6,1.0) xzoom -1
        with fade
        show p1 with moveinleft:
            align (0.0,1.0) xpos 0.0 xzoom -1
        "Lia dotarła po dłuższym spacerze w okolicę tego Monumentu o którym wszyscy mówią."
        "Z daleka dostrzegła jednak, że całość jest jeszcze zasłonięta, gdy miała zawracać to usłyszała w tle głosy..."
        pa "... taaaak? Odsłonięcia na dwudziestego mu się zachciało tak? PROSZĘ MU POWIEDZ..."
        strzmon "Ależ Pani Architekt, proszę nie krzyczeć. Ja tylko pilnuję i przekazuję wiadomość."
        pa "Tak? To proszę przekazać wiadomość, żeby się tam w łeb ktoś mocno puknął."
        strzmon "Dobrze Pani wie, że nie mogę przekazać takiej wiadomości."
        strzmon "Powiem jednak, że sugerowała Pani przeanalizowanie raz jeszcze terminu odsłonięcia."
        pa "Chociaż coś. Na co PAN czeka w takim razie?"
        strzmon "Mój zmiennik będzie dopiero wieczorem, wcześniej nie mogę zejść ze stanowiska..."
        pa "A CHOLERA BY WAS I WASZE ZASADY, sama pójdę!"
        with vpunch
        show architect with move:
            xalign 0.3 xzoom 1
        "Lia zauważyła, że niewysoka Pani Architekt tupnęła tylko nogą na zakończenie rozmowy i skierowała się w jej stronę."
        "Zbliżyła się dość szybko, zatrzymała się, zmierzyła wzrokiem Lię po czym zapytała."
        pa "A Ty dziewczyno co tutaj robisz co? Skąd się wzięłaś?"
        p1 "Z Tawerny prze Pani... Znaczy mieszkam tam i pracuję. Chciałam zobaczyć..."
        pa "Nieskończony jeszcze, nie widać? Ach, przepraszam. Zdenerwowana jestem."
        show architect -angry with Dissolve(0.2)
        pa "Jak widzisz jeszcze nie jest zakończony, termin premiery ustawiony jest zdecydowanie zbyt wcześnie. Pytania?"


        menu demo_mon_poc:
            "Dlaczego za wcześnie?":
                "Zapytała Lia i szybko zaczęła żałować tego pytania. Zauważyła jak Architekt powstrzymuje się przed nakrzyczeniem na nią."
                "Rozpoznała to bo jej ojciec robił bardzo podobnie... Myślenie przerwała jednak spokojnym głosem rozmówczyni."
                pa "Bo bardzo mi zależy, żeby wszystko dopracować. Każdy najmniejszy szczegół zasługuje na chwilę uwagi."
                pa "Widzisz... Sztuka w każdej postaci, czy to tego pomniku, czy obrazu czy czegokolwiek innego jest czymś czego nie można zrobić byle jak."
                pa "Jeśli chcesz, żeby ktoś się zatrzymał i spojrzał na to co zrobiłaś z szacunkiem czy nawet z podziwem."
                pa "To wszystko wymaga bardzo dużo pracy..."
                menu demo_mon_1a:
                    "Czy można jakoś pomóc?":
                        pa "Czy można... Hmm, tak. Jest oficjalna zbiórka, można dorzucić się do budowy."
                        pa "{a=https://www.kickstarter.com/projects/gameload/yourstory}Tutaj{/a} masz wszystkie szczegóły."
                        p1 "Dziękuję! Na pewno zobaczę i sprawdzę. Powodzenia w pracy!"
                        $ z_demo_monum_1wejscie = True
                        # Koniec dialogu, ekran wraca na pełny rzut na monument

                    "Rozumiem, to nie przeszkadzam.":
                        pa "Nie przeszkadzałaś... Ah, nieważne. Bezpiecznej drogi. Muszę iść. Do widzenia."
                        $ z_demo_monum_1wejscie = True

            "Nie, nie mam żadnych pytań.":
                pa "Ach, dobrze. To zmykaj. Do widzenia."
                "Lia zauważyła tylko kątem oka jak Pani Architekt czmychnęła obok niej i ruszyła w stronę wioski."
                hide architect with moveoutleft
                $ z_demo_monum_1wejscie = True



    default strzrozmmon1 = False
    default strzrozmmon2 = False
    default z_demo_mon_koniecrozmowy = False

    scene village_monumentcovered with dissolve
    call screen sc_demo_monument with dissolve
    while _return != "mapa":
        #default tekstguardamon = renpy.random.choice(["Warta przy drodze... Też mi... Ahh, witam!", "Jakoś pomóc?", "Hmm... Czego?","Mówiłaś coś?","Cholerne słońce... Eh.. Witam!"])
        $ tekstguardamon = ballgaze()
        if _return == "guard":
            show guard_blue:
                align(0.8,1.0)
            show p1:
                align(0.1,1.0) xzoom -1
            with dissolve
            strzmon "[tekstguardamon!t]"
            $ z_demo_mon_koniecrozmowy = False
            while not z_demo_mon_koniecrozmowy:
                menu:
                    "O co tutaj dokładnie chodzi?":
                        p1 "O co tutaj dokładnie chodzi?"
                        strzmon "Pani Architekt Ci nie powiedziała?"
                        strzmon "Ludzie na wsi nie gadają o tym?"
                        strzmon "Nie jesteś stąd czy jak?"
                        strzmon "No nieważne, to Monument Legend - wszyscy bohaterowie królestwa będą tutaj upamiętnieni."
                        strzmon "Czy to wystarczy?"
                        p1 "Tak... Dziękuję."
                        $ strzrozmmon1 = True


                    "A kiedy odsłonięcie?" if strzrozmmon1:
                        p1 "A kiedy odsłonięcie?"
                        strzmon "Eh.. Kolejna. Nie wiem dokładnie. Zakładam, że inny termin niż był rano."
                        strzmon "Zmieniają to ciągle, raz jest wcześniej, raz jest później."
                        strzmon "Będzie odsłonięty to na pewno każdy zauważy."
                        $ strzrozmmon2 = True


                    "Co właściwie na nim ma być?" if strzrozmmon2:
                        p1 "Co właściwie na nim ma być?"
                        strzmon "Ty słuchałaś co mówiłem czy tak sobie stoisz i tylko ładnie wyglądasz?"
                        strzmon "Ehh... Nie wytrzymam zbyt długo tutaj... Imiona będą. Bohaterów królestwa."
                        strzmon "Wystarczy czy jeszcze jakieś inteligentne pytania?"
                        p1 "Wystarczy... Już sobie idę."
                        hide p1
                        hide guard_blue
                        $ z_demo_mon_koniecrozmowy = True

                    "Pójdę już sobie.":
                        p1 "To ja już chyba sobie pójdę."
                        hide p1
                        hide guard_blue
                        $ z_demo_mon_koniecrozmowy = True

            call screen sc_demo_monument
    jump demo_mapa

            #jump demo_monument # LOOP - wraca do labela monumentu zeby powtorzyc cala rozmowe

screen thankyouforplaying2():
    add "demoend"
    imagebutton auto "demoend_steambutton_%s" focus_mask True action OpenURL("https://store.steampowered.com/app/1247740/Your_Story")
    imagebutton auto "demoend_ksbutton_%s" focus_mask True action OpenURL("https://www.kickstarter.com/projects/gameload/yourstory")

    imagebutton:
        align (0.5,0.93) #lewa
        xpos 0.20
        idle "exit_button_idle"
        hover "exit_button_hover"
        action Return("wyjscie")
    text "EXIT TO MENU" align (0.5,0.919) xpos 0.20 #lewa

    imagebutton:
        align (0.5,0.87) #lewa
        xpos 0.20
        idle "exit_button_idle"
        hover "exit_button_hover"
        action Return("explore")
    text "EXPLORE" align (0.5,0.859) xpos 0.20 #lewa

label demo_tawerna:
    scene anim_room_lia_morning
    show room_lia_letter_idle
    with dissolve
    call screen sc_demo_liaroom
    while _return != "mapa":
        if _return == "letter":
            call screen thankyouforplaying2
        if _return == "wyjscie":
            return
        if _return == "explore":
            call screen sc_demo_liaroom
    jump demo_mapa
